@tool
@icon("res://addons/sprite_dicing/icon.svg")

## Stores diced sprite build settings and generated resources.
class_name DicedSpriteAtlas extends Resource

## A folder with the source sprite textures to dice and bake into this atlas.
@export_dir var input_folder: String
## Atlas textures generated by this atlas.
@export_custom(PROPERTY_HINT_NONE, "", PROPERTY_USAGE_READ_ONLY | PROPERTY_USAGE_DEFAULT)
var atlas_textures: Array[Texture2D] = []
## Diced sprites generated by this atlas.
@export_custom(PROPERTY_HINT_NONE, "", PROPERTY_USAGE_READ_ONLY | PROPERTY_USAGE_DEFAULT)
var diced_sprites: Array[DicedSprite] = []
## Total size of the source textures divided by the size of the generated diced atlas textures 
## plus the associated sprite data size. Higher values mean better compression efficiency.
@export_custom(PROPERTY_HINT_NONE, "", PROPERTY_USAGE_READ_ONLY | PROPERTY_USAGE_DEFAULT)
var compression_ratio: String = "N/A (never built)"
@export var build_button_placeholder: bool

@export_group("Build Settings")
## Whether to recursively search for textures inside the input folder.
@export var include_subfolders: bool = true
## The separator to use when building identifiers for the sprites found in the sub-folders.
@export var separator: String = "."
## Maximum size of a single generated atlas texture; will generate multiple textures when the limit is reached.
@export var atlas_size_limit: int = 2048
## The generated atlas textures will always be square. Less efficient, but required for PVRTC compression.
@export var force_square: bool = false
## The generated atlas textures will always have width and height of power of two.
@export var force_pot: bool = false
## The size of a single diced unit, in pixels.
@export var dice_unit_size: int = 64
## The size of a pixel border to add between adjacent diced units inside atlas to prevent bleeding artifacts.
@export_range(0, 128, 2) var padding: int = 2
## Relative inset of the diced units UV coordinates to prevent texture bleeding artifacts (in addition to padding).
@export_range(0, 0.5, 0.001) var uv_inset: float = 0.0
## Whether to trim transparent areas on the built meshes. Disable to preserve aspect ratio of the source sprites.
@export var trim_transparent: bool = true
## How many pixels in the sprite correspond to the unit in the world.
@export_range(0.001, 1000, 0.001) var pixels_per_unit: float = 1.0
## Origin of the generated diced meshes relative to the bottom-left corner of the sprite rectangle.
@export var default_pivot: Vector2 = Vector2(0.5, 0.5)
## Whether to use pivot set in each individual source sprite (if any), before falling back to the default.
@export var keep_original_pivot: bool = false


func get_sprite(id: String) -> DicedSprite:
    for sprite in diced_sprites:
        if sprite and sprite.sprite_id == id:
            return sprite
    return null


func collect_sprite_ids(ids: PackedStringArray) -> void:
    for sprite in diced_sprites:
        if sprite:
            ids.push_back(sprite.sprite_id)
